if (!isLibrary.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    namespace "com.moko.pirsensor"
    compileSdk 34

    defaultConfig {
        minSdkVersion 26
        targetSdk 34
        versionCode 9
        versionName "1.1.0"
        def formatDate = new Date().format("yyyyMMddHHmm")
        setProperty("archivesBaseName", "MKPIR_${versionName}_${formatDate}")
    }

    buildTypes {
        release {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
        debug {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (!isLibrary.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }

    viewBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    if (isLibrary.toBoolean()) {
        libraryVariants.all { variant ->
            if (variant.buildType.name == "release") {
                // 修改aar名称
                variant.outputs.all { output ->
                    def formatDate = new Date().format("yyyyMMddHHmm")
                    outputFileName = "MKPIR_${android.defaultConfig.versionName}_${formatDate}.aar"
                }
                // 复制aar到指定目录
                variant.assemble.doLast {
                    variant.outputs.all { output ->
                        // 输出目录在：项目目录/build/outputs/aar/
                        def outputPath = [project.rootDir.absolutePath, "build", "outputs", "aar"].join(File.separator)
                        copy {
                            from output.outputFile
                            into outputPath
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    implementation 'com.github.GrenderG:Toasty:1.4.1'
    implementation 'com.github.Carbs0126:NumberPickerView:master-SNAPSHOT'
    implementation 'com.guolindev.permissionx:permissionx:1.7.1'
    if (!isLibrary.toBoolean()) {
        implementation project(path: ':mokosupport')
    } else {
        implementation files('libs/mokoSupport.jar')
        compileOnly 'com.github.MOKO-Android-Base-Library:MKBleLib:1.0.0-beacon'
    }
}
